/* IMO 2019, problem 2 */

load("geometry.mac");

/* Declare free points A, B, C. */
declare([a, b, c], complex);
/* Declare the ratios for A₁ and B₁. */
declare([ra, rb], real);
/* Declare a free point for PQ. */
declare(k, complex);

/*
 * Choose C as the origin to avoid the following error in Maxima:
 *
 * "PQUOTIENT: Quotient by a polynomial of higher degree (case 2b)"
 */
c : 0;

/* A₁ is on BC. */
a1 : (c - b) * ra + b;
/* B₁ is on CA. */
b1 : (a - c) * rb + c;

/*
 * The line through K parallel to AB intersects AA₁ and BB₁ at
 * P and Q, respectively.
 *
 * An alternative approach is to introduce a ratio with the
 * intersection of AA₁ and BB₁ (see imo2019p2e3.mac).
 */
p : intersection(a, a1, k, (a - b) + k);
q : intersection(b, b1, k, (a - b) + k);

declare([p1, q1], complex);

/* Solve P₁ and Q₁. */
p1s : map(rhs, solve(eliminate(
  /* P, B₁, P₁ are collinear; ∠PP₁C = ∠BAC. */
  [collinear(p, b1, p1),
   pure_real(((p1 - c) / (p1 - p)) / ((a - c) / (a - b)))],
  [conjugate(p1)]), p1));
q1s : map(rhs, solve(eliminate(
  /* Q, A₁, Q₁ are collinear; ∠CQ₁Q = ∠CBA. */
  [collinear(q, a1, q1),
   pure_real(((q1 - c) / (q1 - q)) / ((b - c) / (b - a)))],
  [conjugate(q1)]), q1));
/* Exclude P₁ = P. */
prove(length(p1s) - 2);
prove(second(p1s) - p);
p1 : first(p1s);
/* Exclude Q₁ = Q. */
prove(length(q1s) - 2);
prove(second(q1s) - q);
q1 : first(q1s);

/* Perform sanity checks on P₁ and Q₁. */
prove(collinear(p1, b1, p));
prove(pure_real(((p1 - c) / (p1 - p)) / ((a - c) / (a - b))));
prove(collinear(q1, a1, q));
prove(pure_real(((q1 - c) / (q1 - q)) / ((b - c) / (b - a))));

/* Prove that P, Q, P₁, Q₁ are concyclic. */
prove(concyclic(p, q, p1, q1));

quit();
