/* Another solution to IMO 2019, problem 6 */

load("geometry.mac")$

/* Declare angles for D, E, F. */
declare([theta_d, theta_e, theta_f], real)$

/* D, E, F are on the unit circle. */
d : %e^(%i * theta_d);
e : %e^(%i * theta_e);
f : %e^(%i * theta_f);

/* A, B, C are the intersection points of two tangents. */
a : 2 / conjugate(e + f);
b : 2 / conjugate(f + d);
c : 2 / conjugate(d + e);

/* R is derived from RD ‚ä• EF, with R, D, E, F on the unit circle. */
r : -(e * f / d);

/*
 * P is the reflection of R over the line through O perpendicular
 * to AR.  One may also derive this from the fact that A, R, P are
 * collinear, with R and P on the unit circle.
 */
p : reflection(0, %i * (r - a), r);

/*
 * L is the intersection of DI and the line perpendicular to AI.
 *
 * Compared to computing the intersection of DI and PQ, this is faster
 * as the coordinates of A and I are simpler than those of P and Q.
 * It suffices to show that P, Q, L are collinear.
 */
l : intersection(0, d, a, rotate(0, a, -%pi/2));

/* Ob is the circumcircle of triangle PBF. */
ob : circumcenter(p, b, f);

/* Oc is the circumcircle of triangle PCE. */
oc : circumcenter(p, c, e);

/*
 * Prove that ObOc and PL are perpendicular, which implies that
 * P, Q, L are collinear.
 *
 * This is simpler and faster than computing Q as the reflection of P
 * over ObOc and directly proving that P, Q, L are collinear.
 */
prove(perpendicular(ob, oc, p, l));

quit();
